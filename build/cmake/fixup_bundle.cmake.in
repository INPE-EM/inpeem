include(BundleUtilities)

if(WIN32)
	set(INPEEM_APP_PATH ${CMAKE_INSTALL_PREFIX}/@PROJECT_NAME@.exe)
	file(GLOB INPEEM_LIBS ${CMAKE_INSTALL_PREFIX}/*@CMAKE_SHARED_LIBRARY_SUFFIX@)
	set(INPEEM_PATHS ${CMAKE_INSTALL_PREFIX};@Qt5_DIR@/../../../bin)
elseif(APPLE)
	set(BU_CHMOD_BUNDLE_ITEMS ON)
	set(INPEEM_APP_PATH ${CMAKE_INSTALL_PREFIX}/@PROJECT_NAME@.app)
	set(INPEEM_QT_PLUGINS_ABSOLUTE_LOCATION ${CMAKE_INSTALL_PREFIX}/@INPEEM_QT_PLUGINS_LOCATION@)
	file(GLOB_RECURSE QT_PLUGINS "${INPEEM_QT_PLUGINS_ABSOLUTE_LOCATION}/*@CMAKE_SHARED_LIBRARY_SUFFIX@")
	set(INPEEM_LIBS ${INPEEM_LIBS} ${QT_PLUGINS})
	#message("${QT_PLUGINS}---------------------${INPEEM_QT_PLUGINS_ABSOLUTE_LOCATION}")
	#exit()
	set(INPEEM_PATHS ${CMAKE_INSTALL_PREFIX};@Qt5_DIR@/../..)
endif()

if(APPLE)
	# Overload function
	function(gp_item_default_embedded_path item default_embedded_path_var)
		set(is_System FALSE)
		set(is_QImageFormat FALSE)
		set(is_QIconEngine FALSE)
		set(is_QSQLDrivers FALSE)
		set(is_QPlatforms FALSE)
		set(is_Lib FALSE)

		get_filename_component (resolved_file ${item} PATH)

		if("${resolved_file}" STREQUAL "")
			set (is_Lib TRUE)
		elseif(resolved_file MATCHES "^(@rpath)")
			set(is_Lib TRUE)
		elseif(resolved_file MATCHES "^(/System/Library/|/usr/lib|/usr/local|@loader_path)")
			set(is_System TRUE)
		elseif(resolved_file MATCHES "(imageformats)$")
			set (is_QImageFormat TRUE)
		elseif(resolved_file MATCHES "(iconengines)$")
			set (is_QIconEngine TRUE)
		elseif(resolved_file MATCHES "(sqldrivers)$")
			set (is_QSQLDrivers TRUE)
		elseif(resolved_file MATCHES "(platforms)$")
			set (is_QPlatforms TRUE)
		elseif(resolved_file MATCHES "(lib)$")
			set (is_Lib TRUE)
		endif()

		if(item MATCHES "\\.dylib$")
			if(is_System)
				set(${default_embedded_path_var} "@executable_path/../lib" PARENT_SCOPE)
			elseif(is_QImageFormat)
				set(${default_embedded_path_var} "@executable_path/../qtplugins/imageformats" PARENT_SCOPE)
			elseif(is_QIconEngine)
				set(${default_embedded_path_var} "@executable_path/../qtplugins/iconengines" PARENT_SCOPE)
			elseif(is_QSQLDrivers)
				set(${default_embedded_path_var} "@executable_path/../qtplugins/sqldrivers" PARENT_SCOPE)
			elseif(is_QPlatforms)
				set(${default_embedded_path_var} "@executable_path/../qtplugins/platforms" PARENT_SCOPE)
			elseif(is_Lib)
				set(${default_embedded_path_var} "@executable_path/../lib" PARENT_SCOPE)
			else()
				set(${default_embedded_path_var} "${resolved_file}" PARENT_SCOPE)
			endif()
		elseif(item MATCHES "[^/]+\\.framework/")
			set(${default_embedded_path_var} "@executable_path/../Frameworks" PARENT_SCOPE)
		endif()
	endfunction()
endif()

fixup_bundle(${INPEEM_APP_PATH} "${INPEEM_LIBS}" "${INPEEM_PATHS}")
